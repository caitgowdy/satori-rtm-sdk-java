apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.satori'

description = """Satori SDK for Java"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

jar {
    exclude '**/examples/**'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'satori-sdk-java'
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0"
        classpath "net.researchgate:gradle-release:2.4.0"
        classpath "co.riiid:gradle-github-plugin:0.4.2"
    }
}

javadoc {
    exclude '**/examples/**'
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.JFlags = ["-Xmx512m", '-XX:MaxPermSize=256m']
}

test {
    testLogging.showStandardStreams = true

    minHeapSize = "128m"
    maxHeapSize = "512m"
    maxParallelForks = 1

    jvmArgs '-XX:MaxPermSize=256m'

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    systemProperties = [
            RTM_CONFIG: System.getProperty('RTM_CONFIG')
    ]
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'com.neovisionaries', name: 'nv-websocket-client', version: '1.31'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.13'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.0'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.7.0'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.7.0'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.0'
}

if (file('./release.gradle').exists()) {
    apply from: "release.gradle"
}
